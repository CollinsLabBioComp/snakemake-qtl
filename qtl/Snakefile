#!/usr/bin/env snakemake

"""
Snakemake QTL pipeline
----------------------

Snakemake pipeline for molecular trait QTL mapping
"""

__version__ = '0.1.4'

rule all_qtl:
    input:
        # QTLtools nominal associations
        'qtltools_nominal.tsv.gz',
        
        # QTLtools permutation associations
        'qtltools_permute.tsv.gz',
        'plots/qtltools_permute-check_beta_approx.png',

        # QTLtools conditional associations
        'qtltools_conditional.tsv.gz',

        # veqtl-mapper variance qtls
        'vqtlmapper_permute-top_qtl.tsv.gz'


# QTLtools: file prep ##########################################################
rule qtltools_prep__vcf:
    """
    QTLtools:   generate tabix index of vcf input files 
    """
    input:
        'data/genotypes.vcf.gz'
    output:
        'data/genotypes.vcf.gz.tbi'
    singularity:
        config['DOCKER']
    shell:
        'tabix {input}'


rule qtltools_prep__bed:
    """
    QTLtools:   generate tabix index of bed input files 
    """
    input:
        'data/moltraits.bed.gz'
    singularity:
        config['DOCKER']
    output:
        'data/moltraits.bed.gz.tbi'
    shell:
        'tabix {input}'
################################################################################


# QTLtools: nominal pass #######################################################
rule qtltools_proximal__nominal:
    """
    QTLtools:   nominal proximal mapping (association with all variants)
    """
    input:
        geno='data/genotypes.vcf.gz',
        geno_tbi='data/genotypes.vcf.gz.tbi',
        pheno='data/moltraits.bed.gz',
        pheno_tbi='data/moltraits.bed.gz.tbi'
    output:
        temp('swarm_qtltools_nominal/qtltools_nominal-{j_cur}_{j_total}.txt')
    singularity:
        config['DOCKER']
    params:
        other_param=config['PARAM_QTLTOOLS'],
        nominal_cutoff=config['PARAM_QTLTOOLS_NOMINAL_CUTOFF']
    shell:
        'QTLtools cis --vcf {input.geno} --bed {input.pheno} '
            '--nominal {params.nominal_cutoff} ' 
            '--normal ' # force the input phenotype to N(0, 1) 
            '--seed 15112011 ' # seed for random number generator
            '--window 1000000 ' # window size. default = 1Mb
            '--chunk {wildcards.j_cur} {wildcards.j_total} ' # for swarming
            '{params.other_param} ' # PARAM from config
            '--out {output}'

rule qtltools_proximal__nominal_concat:
    """
    QTLtools: swarm proximal mapping & combine the output
    
    Notes:
    - 1 based so first iteration is 1 not 0.
    - removes the temp input data
    """
    input:
        expand(
            'swarm_qtltools_nominal/qtltools_nominal-{j_cur}_{j_total}.txt', 
            j_cur=range(1, config['NJOBS_NOMINAL']+1), 
            j_total=config['NJOBS_NOMINAL']
        )
    output:
        'qtltools_nominal.tsv.gz'
    shell:
        # add the header to the output file
        'echo "pheno_id '
            'pheno_chr pheno_start pheno_end pheno_strand '
            'n_proximal_var pheno_var_dist '
            'var_id var_chr var_start var_end '
            'p_nominal beta top_proximal_var" | '
        'sed s/" "/"\t"/g | gzip -c > {output}; '
            
        # cat swarm files to make the output file
        'cat {input} | sed s/" "/"\t"/g | gzip -c >> {output}; '
        
        # remove the temp files and swarm directory
        #'rm {input}; ' # no need to by defining temp() in previous rule
        'rm -r swarm_qtltools_nominal; '
################################################################################


# QTLtools: permute pass #######################################################
rule qtltools_proximal__permute:
    """
    QTLtools:   proximal mapping where genotypes are permuted and top proximal 
                variant returned
                
    Notes:
    - only cases where the first qtl is < threshold are in the output
    """
    input:
        geno='data/genotypes.vcf.gz',
        geno_tbi='data/genotypes.vcf.gz.tbi',
        pheno='data/moltraits.bed.gz',
        pheno_tbi='data/moltraits.bed.gz.tbi'
    output:
        temp('swarm_qtltools_permute/qtltools_permute-{j_cur}_{j_total}.txt')
    singularity:
        config['DOCKER']
    params:
        other_param=config['PARAM_QTLTOOLS'],
        n_perm=config['NPERM']
    shell:
        'QTLtools cis --vcf {input.geno} --bed {input.pheno} '
            '--permute {params.n_perm} ' # qtltools not have adaptive perm
            '--normal ' # force the input phenotype to N(0, 1) 
            '--seed 15112011 ' # seed for random number generator
            '--window 1000000 ' # window size. default = 1Mb
            '--chunk {wildcards.j_cur} {wildcards.j_total} ' # for swarming
            '{params.other_param} ' # PARAM from config
            '--out {output}'


rule qtltools_proximal__permute_concat:
    """
    QTLtools:   swarm proximal mapping & combine the output
    
    Notes:
    - 1 based so first iteration is 1 not 0.
    - removes the temp input data
    """
    input:
        expand(
            'swarm_qtltools_permute/qtltools_permute-{j_cur}_{j_total}.txt',
            j_cur=range(1, config['NJOBS_PERMUTE']+1), 
            j_total=config['NJOBS_PERMUTE']
        )
    output:
        'qtltools_permute.tsv.gz'
    shell:
        # add the header to the output file        
        'echo "pheno_id '
            'pheno_chr pheno_start pheno_end pheno_strand '
            'n_proximal_var pheno_var_dist '
            'var_id var_chr var_start var_end '
            'p_degree_freedom '
            'dummy '
            'beta_dist_par1 beta_dist_par2 '
            'p_nominal beta '
            'p_empirical p_adjust_beta_dist" | '
        'sed s/" "/"\t"/g | gzip -c > {output}; '
            
        # cat swarm files to make the output file
        'cat {input} | sed s/" "/"\t"/g | gzip -c >> {output}; '
        
        # remove the temp swarm directory
        #'rm {input}; '
        'rm -r swarm_qtltools_permute; '

rule qtltools_proximal__permute_fdr:
    """
    QTLtools:   run the FDR script for the permuted data
    """
    input:
        'qtltools_permute.tsv.gz'
    output:
        'qtltools_permute-significant.tsv.gz',
        'qtltools_permute-thresholds.txt.gz'
    singularity:
        config['DOCKER']
    params:
        shell_script=srcdir('scripts/qtltools-runFDR_cis.R')
    shell:
        'Rscript {params.shell_script} {input} 0.05 `basename {input} .tsv.gz`'

rule qtltools_proximal__plot__check_beta_approx:
    input:
        'qtltools_permute.tsv.gz'
    output:
        'plots/qtltools_permute-check_beta_approx.png',
        'plots/qtltools_permute-check_beta_approx-neglog10.png'
    singularity:
        config['DOCKER']
    params:
        shell_script=srcdir('scripts/qtltools-check_beta_approx.R')
    shell:
        'Rscript {params.shell_script} {input} '
            'plots/`basename {input} .tsv.gz`-check_beta_approx'
################################################################################


# QTLtools: conditional pass ###################################################
rule qtltools_proximal__conditional_unzip_threshold:
    """
    QTLtools requires uncompressed thresholds file.
    """
    input:
        'qtltools_permute-thresholds.txt.gz'
    output:
        'qtltools_permute-thresholds.txt'
    shell:
        'gunzip -c {input} > {output}'
        
rule qtltools_proximal__conditional:
    """
    QTLtools:   conditional proximal mapping using thresholds from permuted data
    """
    input:
        geno='data/genotypes.vcf.gz',
        geno_tbi='data/genotypes.vcf.gz.tbi',
        pheno='data/moltraits.bed.gz',
        pheno_tbi='data/moltraits.bed.gz.tbi',
        pheno_thresh='qtltools_permute-thresholds.txt'
    output:
        temp('swarm_qtltools_conditional/qtltools_conditional-{j_cur}_{j_total}.txt')
    singularity:
        config['DOCKER']
    params:
        other_param=config['PARAM_QTLTOOLS']
    shell:
        'QTLtools cis --vcf {input.geno} --bed {input.pheno} '
            '--mapping {input.pheno_thresh} ' # thresholds
            '--normal ' # force the input phenotype to N(0, 1) 
            '--seed 15112011 ' # seed for random number generator
            '--window 1000000 ' # window size. default = 1Mb
            '--chunk {wildcards.j_cur} {wildcards.j_total} ' # for swarming
            '{params.other_param} ' # PARAM from config 
            '--out {output}'

rule qtltools_proximal__conditional_concat:
    """
    QTLtools:   swarm proximal mapping & combine the output
    
    Notes:
    - 1 based so first iteration is 1 not 0.
    - removes the temp input data
    """
    input:
        expand(
            'swarm_qtltools_conditional/qtltools_conditional-{j_cur}_{j_total}.txt',
            j_cur=range(1, config['NJOBS_CONDITIONAL']+1), 
            j_total=config['NJOBS_CONDITIONAL']
        )
    output:
        'qtltools_conditional.tsv.gz'
    shell:
        # add the header to the output file        
        'echo "pheno_id '
            'pheno_chr pheno_start pheno_end pheno_strand '
            'n_proximal_var pheno_var_dist '
            'var_id var_chr var_start var_end '
            'var_rank ' # if variant = 1st best (rank=0), 2nd best (rank=1)
            'p_nominal_forward beta_forward '
            'var_top_rank_forward var_p_below_threshold_forward '
            'p_nominal_backward beta_backward '
            'var_top_rank_backward var_p_below_threshold_backward" | '
        'sed s/" "/"\t"/g | gzip -c > qtltools_conditional.tsv.gz; '
        
        
        # cat swarm files to make the output file
        'cat {input} | sed s/" "/"\t"/g | '
            'gzip -c >> qtltools_conditional.tsv.gz; '
        
        
        # compress the thresholds file
        'gzip -f qtltools_permute-thresholds.txt; '
        
        # remove the temp swarm directory
        #'rm {input}; '
        'rm -r swarm_qtltools_conditional; '
################################################################################


# vqtl-mapper: file prep ######################################################
rule vqtlmapper_prep__bed_gz:
    """
    vqtl-mapper:    change bed input file to be consistent with fastqtl and 
                    vqtl-mapper
    """
    input:
        'data/moltraits.bed.gz'
    output:
        'data/moltraits-fastqtl.bed.gz'
    singularity:
        config['DOCKER']
    shell:
        'gunzip -c {input} | cut -f 1-4,7- | bgzip -c > '   
            'data/moltraits-fastqtl.bed.gz; '

rule vqtlmapper_prep__bed:
    """
    vqtl-mapper:    change bed input file to be consistent with fastqtl and 
                    vqtl-mapper
    """
    input:
        'data/moltraits-fastqtl.bed.gz'
    output:
        'data/moltraits-fastqtl.bed'
    shell:
        'gunzip -c {input} > data/moltraits-fastqtl.bed'
        
rule vqtlmapper_prep__bed_tabix:
    """
    vqtl-mapper:    change bed input file to be consistent with fastqtl and 
                    vqtl-mapper
    """
    input:
        'data/moltraits-fastqtl.bed.gz'
    output:
        'data/moltraits-fastqtl.bed.gz.tbi'
    singularity:
        config['DOCKER']
    shell:
        'tabix {input}'
################################################################################

# vqtl-mapper: permutation pass ###############################################
rule vqtlmapper__permute:
    """
    vqtl-mapper:   map variance qtls
    """
    input:
        geno='data/genotypes.vcf.gz',
        geno_tbi='data/genotypes.vcf.gz.tbi',
        pheno='data/moltraits-fastqtl.bed'
    output:
        temp('swarm_vqtl_permute/vqtl_permute-{j_cur}_{j_total}.txt')
    singularity:
        config['DOCKER']
    params:
        perm=config['NPERM'],
        seed='15112011',
        window='1000000',
        other_settings='--normal'
    script:
        'wrappers/veqtl-mapper/wrapper.py'

rule vqtlmapper__permute_concat:
    """
    vqtl-mapper:   swarm proximal mapping & combine the output
    
    Notes:
    - 1 based so first iteration is 1 not 0.
    - removes the temp input data
    - corrected for multiple testing across proximal variants per feature
    """
    input:
        expand('swarm_vqtl_permute/vqtl_permute-{j_cur}_{j_total}.txt',
            j_cur=range(1, config['NJOBS_PERMUTE']+1), 
            j_total=config['NJOBS_PERMUTE'])
    output:
        'vqtlmapper_permute.tsv.gz'
    shell:            
        # cat swarm files to make the output file
        'awk "FNR>1||NR==1" {input} | sed s/"GENE"/"pheno_id"/ | '
            'gzip -c > {output}; '
        
        # remove the temp swarm directory
        #'rm {input}; '
        'rm -r swarm_vqtl_permute; '
        
        # remove the temporary bed file (we already have gzipped copy)
        'rm data/moltraits-fastqtl.bed; '
        
rule vqtlmapper__top_snp:
    """
    vqtl-mapper:    get top QTL per feature and correct for number of molecular
                    traits tested using Storey's FDR.
    """
    input:
        'vqtlmapper_permute.tsv.gz'
    output:
        'vqtlmapper_permute-top_qtl.tsv.gz'
    singularity:
        config['DOCKER']
    params:
        script_tophit=srcdir('../scripts/general-min_line_per_feature.R'),
        script_fdr=srcdir('../scripts/general-storey_fdr.R')
    shell:
        # extract the most significant association for each mol. trait
        'Rscript {params.script_tophit} '
            '--file {input} --id_column pheno_id --value_column P | '
            'Rscript {params.script_fdr} --column BETA --out_column q_beta | '
            'gzip -c > {output}'
################################################################################
